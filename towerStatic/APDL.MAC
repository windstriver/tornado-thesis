FINISH $ /CLEAR
/INPUT, 'MODEL', 'MAC',
FINISH
/TITLE, "ANALYSIS OF TOWER"

SEL_TOR = 1
R = 100   ! radical position of the tornado center
THETA = 0
! READ TORNADO WIND FIELD
*DIM, Vt, TABLE, 100, 2, , 'radical', 'height'
*TREAD, Vt, 'tangential_velocity', 'csv', 'InputData/', 2
*DIM, Vr, TABLE, 100, 2, , 'radical', 'height'
*TREAD, Vr, 'radial_velocity', 'csv', 'InputData/', 2
*DIM, Va, TABLE, 100, 2, , 'radical', 'height'
*TREAD, Va, 'axial_velocity', 'csv', 'InputData/', 2

LS = 4000 ! length scale factor
VS = 14 ! velocity scale factor
FINISH


/PREP7
/VUP, 1, Z
/VIEW, 1, 1, 1, 1
EPLOT

ET, 2, BEAM188
ESEL, ALL
EMODIF, ALL, TYPE, 2

NSEL, ALL
NUMMRG, NODE
NSEL, S, LOC, Z, 0-SEL_TOR, 0+SEL_TOR
D, ALL, ALL, 0
ALLSEL, ALL

! MODAL ANALYSIS
/SOLU
ANTYPE, MODAL
MODOPT, LANB, 9
MXPAND, 9
SOLVE
FINISH

/POST1
SET, LIST
!SET, FIRST
!PLNSOL, U, SUM, 0, 1.0
!ANMODE, 10, 0.5
!SET, NEXT
!PLNSOL, U, SUM, 0, 1.0
!ANMODE, 10, 0.5
FINISH

WLP_NUM = 4   ! number of wind load points
SEC_NUM = 1   ! number of floor sections

/PREP7
! Get wind load points
*DIM, WLP, TABLE, WLP_NUM, 3, , 'NO.', 'XYZ'
*TREAD, WLP, 'wind_load_points', 'csv', 'InputData/', 2
! Get full scale tornado wind velocity at global coordinates at wlp
*DIM, PV, ARRAY, WLP_NUM, 3 ! array to store tornado velocity components
*DO, I, 1, WLP_NUM
    x_i = WLP(I,1)/1000 ! get x of I and convert it to meter
    y_i = WLP(I,2)/1000
    z_i = WLP(I,3)/1000
    R_fi = SQRT((x_i-R*COS(THETA))**2+(y_i-R*SIN(THETA))**2)
    R_ma = R_fi/LS
    Z_ma = z_i*1.0/LS
    V_t = Vt(R_ma,Z_ma)*VS
    V_r = Vr(R_ma,Z_ma)*VS
    V_a = Va(R_ma,Z_ma)*VS
    PV(I, 1) = -V_t*(y_i-R*SIN(THETA))/R_fi + V_r*(x_i-R*COS(THETA))/R_fi
    PV(I, 2) = V_t*(x_i-R*COS(THETA))/R_fi + V_r*(y_i-R*SIN(THETA))/R_fi
    PV(I, 3) = V_a
*ENDDO

! Get projected area
YITA = 1.0
MU_S = 1.3*(1+YITA)
RHO_A = 1.225
*DIM, PA, TABLE, SEC_NUM, 3, , 'LEVEL', 'AXYZ'
*TREAD, PA, 'projected_area', 'csv', 'InputData/', 2
! APPLY TORNADO WIND LOAD
*DO, I, 1, SEC_NUM
    VX_AVG = (PV(4*I-3,1)+PV(4*I-2,1)+PV(4*I-1,1)+PV(4*I,1))/4
    VY_AVG = (PV(4*I-3,2)+PV(4*I-2,2)+PV(4*I-1,2)+PV(4*I,2))/4
    FX_T = 0.5*RHO_A*VX_AVG**2*MU_S*PA(I,1)
    FY_T = 0.5*RHO_A*VX_AVG**2*MU_S*PA(I,2)
    WLN1 = NODE(WLP(4*I-3,1),WLP(4*I-3,2),WLP(4*I-3,3))
    WLN2 = NODE(WLP(4*I-2,1),WLP(4*I-2,2),WLP(4*I-2,3))
    WLN3 = NODE(WLP(4*I-1,1),WLP(4*I-1,2),WLP(4*I-1,3))
    WLN4 = NODE(WLP(4*I,1),WLP(4*I,2),WLP(4*I,3))
    F, WLN2, FX, 0.5/(1+YITA)*FX_T
    F, WLN3, FX, 0.5/(1+YITA)*FX_T
    F, WLN4, FX, 0.5*YITA/(1+YITA)*FX_T
    F, WLN1, FX, 0.5*YITA/(1+YITA)*FX_T
*ENDDO
