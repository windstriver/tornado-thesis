\chapter{计算输电线对输电塔荷载的APDL和MATLAB程序}\label{apen:cat}
\section{计算跨越塔之间输电线对跨越塔水平荷载的 ANSYS APDL 命令流}

\begin{minted}{apdl}

! UNITS: m-kN
FINISH
/CLEAR
/PREP7
EA=729E-6                                ! 单缆面积(m^2)
EE=1.0807E8                              ! 弹性模量(kN/m^2)
QQ=0.0315462                             ! 单缆每延米重量(kN/m)
YSM0=132.4                               ! 中跨矢高(m)
CH=0                                     ! 中跨锚点高差(m)
SPAN=1770                                ! 跨径
                                         
ET,1,LINK1                               ! 定义单元
R,1,EA,1.0E-5                            ! 设置一个较小的初应变
MP,EX,1,EE                               ! 定义材料特性
MP,PRXY,1,0.3
MP,DENS,1,43.273

! 根据抛物线理论计算水平力迭代初始值
FF=YSM0-CH/2
HH=QQ*SPAN**2/(8*FF)                     ! 计算水平力
HH1=0.9*HH                               ! 设置水平力迭代区间
HH2=1.1*HH

! 用二分法迭代计算主缆水平力
*DO,I,1,100,1
    HFM=(HH1+HH2)/2                      ! 水平力迭代初值
    CI=QQ/HFM                            ! 中间参数
    A0=CH*CI/SINH(SPAN*CI/2)/2           ! 中间参数
    AI=LOG(A0+SQRT(A0**2+1))+SPAN*CI/2   ! 中间参数alpha
    BI=COSH(AI)/CI                       ! 中间参数alpha1
    YSM=BI-COSH(CI*(SPAN/2)-AI)/CI       ! 计算跨中矢高
    *IF,YSM,GT,YSM0,THEN                 ! 修正水平力
        HH1=HFM
    *ELSEIF,YSM,LE,YSM0
        HH2=HFM
    *ENDIF
*ENDDO
ERROR1=(YSM-YSM0)/YSM0                   ! 跨中处索垂度误差
MSS=(SINH(CI*SPAN-AI)+SINH(AI))/CI       ! 按悬链线方程得出的形状长度
DSO=HFM*(SPAN+(SINH(2*CI*SPAN-2*AI)+SINH(2*AI))/2/CI)/(2*EE*EA)
                                         ! 按悬链线方程得出的伸长量
YBM=DSO/(MSS-DSO)                        ! 中跨的初始应变
R,1,EA,YBM                               ! 修改单缆初始应力
EL=1 ! 单元水平长度
ENU=SPAN/EL+1
*DIM,X,ARRAY,ENU,1
*DIM,Y,ARRAY,ENU,1
*DO,I,1,ENU,1                            ! 定义单缆节点
    X(I)=(I-1)*EL
    Y(I)=BI-COSH(CI*X(I)-AI)/CI
    N,I,X(I),-Y(I),0
*ENDDO
TYPE,1
MAT,1
REAL,1
ESYS,0                                    ! 单缆单元
*DO,I,1,ENU-1,1
    E,I,I+1
*ENDDO
EPLOT
FINISH

/SOLU
D,1,ALL                                   ! 定义边界条件
D,ENU,ALL
ACEL,0,1,0                                ! 施加重力荷载
ANTYPE,STATIC
NLGEOM,ON
NROPT,AUTO
LUMPM,OFF
EQSLV,,,0
AUTOTS,OFF
NSUBST,100

KBC,0

ALLSEL
SOLVE

/POST1
PLNSOL,U,Y

\end{minted}

\section{计算跨越塔之间输电线对跨越塔竖向荷载的 MATLAB 程序}
\begin{minted}{matlab}
L = 1770;
C = 0;
q_y = 0.0315462;
Tx = 93.3072979;
beta = q_y*L/(2*Tx);
c1 = asinh((beta*C/L)/sinh(beta))-beta;
c2 = -Tx/q_y*cosh(c1);
cat = @(x) Tx/q_y*cosh(q_y/Tx*x+c1)+c2;
cat_d = @(x) sinh(q_y/Tx*x+c1);
x = 0:L;
y = cat(x);
csvwrite('cat.csv',horzcat(transpose(x/100), transpose(y/100)))
mid_def = cat(L/2);
theta = cat_d(L);
plot(x,y)
hold on
axis equal

\end{minted}